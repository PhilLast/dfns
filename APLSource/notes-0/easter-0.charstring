yyyymmdd ← ##.easter year                   ⍝ Easter Sunday in year ⍵.

In 325 A.D. the Council of Nicaea decreed that Easter was to fall upon the first
Sunday after the first full moon on or after the Vernal Equinox.

Suggested by John R. Clark, this algorithm is taken from D.E.Knuth's "The Art of
Computer Programming - Fundamental Algorithms" Vol 1, Ch 1.3.2 Ex14.

Knuth says: "The algorithm, due to the Neapolitan astronomer Aloysius Lilius and
the  German Jesuit mathematician Christopher Clavius in the late 16th century is
used  by  most  Western  churches to determine the date of Easter Sunday for any
year  after  1582.  The  first  systematic algorithm for calculating the date of
Easter was the "canon paschalis" due to Victorius of Aquitania (457 A.D.). There
are many indications that the sole important application of arithmetic in Europe
during  the  Middle  Ages was the calculation of Easter date, and so such algor-
ithms are historically significant."

Technical note:

As all of the functions used in the coding of this function are "pervasive", it-
just-works on arrays of higher rank and depth.

Examples:

      easter⊃⎕ts                            ⍝ This year's Easter.
20040411

      0 100 100⊤easter 1984                 ⍝ ⎕ts-style Easter.
1984 4 22

      easter ⍉4 10⍴2000 to 2039             ⍝ Easter tables for four decades.
20000423 20100404 20200412 20300421
20010415 20110424 20210404 20310413
20020331 20120408 20220417 20320328
20030420 20130331 20230409 20330417
20040411 20140420 20240331 20340409
20050327 20150405 20250420 20350325
20060416 20160327 20260405 20360413
20070408 20170416 20270328 20370405
20080323 20180401 20280416 20380425
20090412 20190421 20290401 20390410

      easter(1800 1900)(2000 2100)          ⍝ nested array of years.
┌─────────────────┬─────────────────┐
│18000413 19000415│20000423 21000328│
└─────────────────┴─────────────────┘

      ⍝ We can make an ISO-Date formatter for such (nested) arrays of dates:

      fmt←{↑,∘('-'∘,)/¯4 ¯2 ¯2↑¨'0',∘⍕¨0 100 100⊤⍵}¨saw

      fmt easter ,∘⊂/1900+⍳4                ⍝ nested Easter Days.
┌─────────────────────────────────────────────────────┐
│┌────────────┬──────────────────────────────────────┐│
││┌──────────┐│┌────────────┬───────────────────────┐││
│││1901-04-07│││┌──────────┐│┌──────────┬──────────┐│││
││└──────────┘│││1902-03-30│││1903-04-12│1904-04-03││││
││            ││└──────────┘│└──────────┴──────────┘│││
││            │└────────────┴───────────────────────┘││
│└────────────┴──────────────────────────────────────┘│
└─────────────────────────────────────────────────────┘

      ⍕fmt easter ⍉6 10⍴2000 to 2059        ⍝ 60 Easters ...
 2000-04-23  2010-04-04  2020-04-12  2030-04-21  2040-04-01  2050-04-10
 2001-04-15  2011-04-24  2021-04-04  2031-04-13  2041-04-21  2051-04-02
 2002-03-31  2012-04-08  2022-04-17  2032-03-28  2042-04-06  2052-04-21
 2003-04-20  2013-03-31  2023-04-09  2033-04-17  2043-03-29  2053-04-06
 2004-04-11  2014-04-20  2024-03-31  2034-04-09  2044-04-17  2054-03-29
 2005-03-27  2015-04-05  2025-04-20  2035-03-25  2045-04-09  2055-04-18
 2006-04-16  2016-03-27  2026-04-05  2036-04-13  2046-03-25  2056-04-02
 2007-04-08  2017-04-16  2027-03-28  2037-04-05  2047-04-14  2057-04-22
 2008-03-23  2018-04-01  2028-04-16  2038-04-25  2048-04-05  2058-04-14
 2009-04-12  2019-04-21  2029-04-01  2039-04-10  2049-04-18  2059-03-30

⍝ Here is a histogram of the frequency of one million Easter dates.
⍝ The numbers in the first column are dates, eg: 322 means March 22.

    hist←{                          ⍝ Histogram of items of vector ⍵.
        ⍺←60                        ⍝ default no of cols.
        sort←{⍵[⍋⍵]}                ⍝ vector sort.
        uniq←sort∪⍵                 ⍝ unique values.
        hits←+/uniq∘.=⍵             ⍝ number of items per distinct value.
        bars←⌈⍺×hits÷⌈/hits         ⍝ bar lengths.
        uniq,' ⎕'[⎕io+bars∘.≥⍳⍺]    ⍝ histogram by unique item.
    }

    hist 1e4|easter 1582+⍳1e6
322 ⎕⎕⎕⎕⎕⎕⎕⎕
323 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
324 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
325 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
326 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
327 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
328 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
329 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
330 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
331 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
401 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
402 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
403 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
404 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
405 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
406 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
407 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
408 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
409 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
410 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
411 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
412 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
413 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
414 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
415 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
416 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
417 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
418 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
419 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
420 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
421 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
422 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
423 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
424 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
425 ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕

See also: Dates saw

Index:date|Nicaea, Council of|canon paschalis|pervasive function|histogram
Index:Easter|array, nested
Index;Clark J.R.|Knuth D.E.|Lilius A.|Clavius C.|Aquitania, Victorius of
